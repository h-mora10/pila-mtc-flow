pre{
	var a: ModelMM!Architecture;
	var bm: ModelMM!BusinessModel;
	
	var dataTypes = new Sequence;
	
	dataTypes.add(new ModelMM!String);
	dataTypes.add(new ModelMM!Integer);
	dataTypes.add(new ModelMM!Float);
	dataTypes.add(new ModelMM!Boolean);
	dataTypes.add(new ModelMM!Date);
	dataTypes.add(new ModelMM!Enum);
	
	PilaValidationsMM!PilaValidations.all.collect(t|t.getArchitecture());
}

rule ServiceOp2Entity
	transform
		s:GrammarMM!ServiceOperator
	to
		t:ModelMM!Entity{
			bm.entities.add(t);
			t.name = s.name;
			t.childrenEntities = s.superEntities.equivalent();
			t.childrenEntities.addAll(s.services.equivalent());
			t.attributes = s.attributes.equivalent();
		}
		
rule SuperEnt2Entity
	transform
		s:GrammarMM!SuperEntity
	to
		t:ModelMM!Entity{
			t.name = s.name;
			t.childrenEntities = s.entities.equivalent();
			t.childrenEntities.add(s.pays.equivalent());
			t.attributes = s.attributes.equivalent();
		}
		
rule BusinessEnt2Entity
	transform
		s:GrammarMM!BusinessEntity
	to
		t:ModelMM!Entity{
			t.name = s.name;
			t.attributes = s.attributes.equivalent();
		}
		
rule Pay2Entity
	transform
		s:GrammarMM!Pay
	to
		t:ModelMM!Entity{
			t.name = s.name;
			t.attributes = s.attributes.equivalent();
		}
		
rule Service2Entity
	transform
		s:GrammarMM!Service
	to
		t:ModelMM!Entity{
			t.name = s.name;
			t.attributes = s.displayName.equivalent();
			t.methods = s.calculate.equivalent();
		}
		
rule Calculate2Method
	transform
		s:GrammarMM!Calculate
	to
		t:ModelMM!Method{
			t.name = s.name;
			t.body = s.toBody();
		}
				
rule StringAttr2Attr
	transform
		s:GrammarMM!StringValue
	to
		t:ModelMM!Attribute{
			t.name = s.name.replace("\"","");
			t.attType = dataTypes[0];
		}

rule IntegerAttr2Attr
	transform
		s:GrammarMM!IntegerValue
	to
		t:ModelMM!Attribute{
			t.name = s.name.replace("\"","");
			t.attType = dataTypes[1];
		}

rule BooleanAttr2Attr
	transform
		s:GrammarMM!BooleanValue
	to
		t:ModelMM!Attribute{
			t.name = s.name.replace("\"","");
			t.attType = dataTypes[3];
		}
		
rule FloatAttr2Attr
	transform
		s:GrammarMM!FloatValue
	to
		t:ModelMM!Attribute{
			t.name = s.name.replace("\"","");
			t.attType = dataTypes[2];
		}
		
rule DateAttr2Attr
	transform
		s:GrammarMM!Date
	to
		t:ModelMM!Attribute{
			t.name = s.name.replace("\"","");
			t.attType = dataTypes[4];
		}
		
rule EnumAttr2Attr
	transform
		s:GrammarMM!Enum
	to
		t:ModelMM!Attribute{
			t.name = s.name;
			t.value = s.items;
			t.attType = dataTypes[5];
		}

rule ComplexAttr2Entity
	transform
		s:GrammarMM!Complex
	to
		t:ModelMM!Entity{
			t.name = s.name.replace("\"","").firstToUpperCase();
			t.attributes = s.attributes.equivalent();			
		}
			
operation GrammarMM!Calculate toBody(): ModelMM!Body{
	var body : new ModelMM!Body;
	var contents : String;
	var level : Integer;
	level = 0;
	for (cond in self.conditions){
		contents = cond.transformStatement(level);
	}
	body.content = contents;
	//body.content.println();
	return body;
}

operation GrammarMM!Statement transformStatement(level : Integer) : String{
	var contents : String;
	
	if (self.isTypeOf(GrammarMM!If)){
		contents += self.transformIf();
	}
	if (self.isTypeOf(GrammarMM!Variable)){
		contents += self.name;
		contents += " = ";
		contents += self.value;
		contents += "\n";
	}
	if (self.isTypeOf(GrammarMM!Return)){
		contents += "return ";
		contents += self.value.transformOperation();
		contents += "\n";
	}
	
	return contents;
}

operation GrammarMM!If transformIf() : String{
	var contents : String;
	var hasLogicOp : Boolean = false;
	var logicOpSeq : Sequence;
	var logicOpIt : Integer = 0;
	
	contents += "if ";
	
	if(self.expressions.size()>1){
		hasLogicOp = true;
		logicOpSeq = self.logoperator;
	}
	
	if(not self.expressions.isEmpty()){
		for (exp in self.expressions){
			contents += exp.transformExpression();
			if(hasLogicOp and (logicOpIt < logicOpSeq.size())){
				contents += " ";
				contents += logicOpSeq.at(logicOpIt);
				contents += " ";
				logicOpIt++;
			}
		}
	}
	
	if(not self.statements.isEmpty()){
		for (st in self.statements){
			contents += st.transformStatement(1);
		}
	}
	
	if(not self.esleIf.isEmpty()){
		for (elf in self.esleIf){
			if(not elf.expressions.isEmpty()){		
				for(exp in elf.expressions){
					contents += exp.transformExpression();
				}
			}
			if(not elf.statements.isEmpty()){
				for(st in elf.statements){
					contents += st.transformStatement(1);
				}
			}
		}
	}
	
	if(self.els.isDefined()){
		for (el in self.els){
			if(not el.statements.isEmpty()){
				for(st in el.statements){
					contents += st.transformStatement(1);
				}
			}
		}
	}

	return contents;
}

operation GrammarMM!Expression transformExpression() : String{
	var contents : String;

	if(self.elementLeft.isDefined()){
		contents += self.elementLeft.element;
		contents += " ";
	}
	if(self.comparator.isDefined()){
		contents += self.comparator;
		contents += " ";
	}
	if(self.elementRight.isDefined()){
		contents += self.elementRight.element;
	}
	contents += "\n";
	
	return contents;
}

operation GrammarMM!Operation transformOperation() : String{
	var contents : String;
	
	if(self.valueLeft.isDefined()){
		contents += self.valueLeft;
		contents += " ";
	}
	if(self.operator.isDefined()){
		contents += self.operator;
		contents += " ";
	}
	if(self.valueRight.isDefined()){
		contents += self.valueRight;
	}
	
	return contents;
}

operation PilaValidationsMM!PilaValidations getArchitecture () : ModelMM!Architecture {
	a = new ModelMM!Architecture;
	
	bm = new ModelMM!BusinessModel;
	bm.entities = self.getEntities();
	bm.dataTypes = dataTypes;
	
	a.businessModel = bm;
	
	return a;
}

operation PilaValidationsMM!PilaValidations getEntities() : Sequence {
	var entities = new Sequence;
	
	if (not self.pagadorpensiones.isEmpty()) {
		var entity = new ModelMM!Entity;
		entity.name = self.pagadorpensiones.get(0).eClass.name;
			
		var attribute = new ModelMM!Attribute;
		attribute.name = 'descripcion';
		attribute.attType = dataTypes[0];
			
		entity.attributes.add(attribute);
		entities.add(entity);
	}
	
	if (not self.tipopensionado.isEmpty()) {
		var entity = new ModelMM!Entity;
		entity.name = self.tipopensionado.get(0).eClass.name;
			
		var attribute = new ModelMM!Attribute;
		attribute.name = 'descripcion';
		attribute.attType = dataTypes[0];
			
		entity.attributes.add(attribute);
		entities.add(entity);
	}
	
	if (not self.tipopension.isEmpty()) {
		var entity = new ModelMM!Entity;
		entity.name = self.tipopension.get(0).eClass.name;
			
		var attribute = new ModelMM!Attribute;
		attribute.name = 'descripcion';
		attribute.attType = dataTypes[0];
			
		entity.attributes.add(attribute);
		entities.add(entity);
	}
	
	if (not self.tipopensionado.get(0).pagadorpensiones.isEmpty()) {
		var entity = new ModelMM!Entity;
		var nombre = self.tipopensionado.get(0).eClass.name + 
		self.tipopensionado.get(0).pagadorpensiones.get(0).eClass.name;
		entity.name = nombre;
			
		var attribute1 = new ModelMM!Attribute;
		attribute1.name = 'tipo_pensionado' + '-' + self.tipopensionado.get(0).eClass.name;
		
		entity.attributes.add(attribute1);
		
		var attribute2 = new ModelMM!Attribute;
		attribute2.name = 'tipo_pagador_pensiones' + '-' + self.tipopensionado.get(0).pagadorpensiones.get(0).eClass.name;
		
		entity.attributes.add(attribute2);
		
		entities.add(entity);
	}
	
	return entities;
}
